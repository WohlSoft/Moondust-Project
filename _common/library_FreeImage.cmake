# FreeImage is an image management library

add_library(PGE_FreeImage INTERFACE)

option(USE_SHARED_FREEIMAGE "Use shared build of FreeImage" OFF)

if(USE_SHARED_FREEIMAGE)
    set(libFreeImage_Libs "${DEPENDENCIES_INSTALL_DIR}/lib/${CMAKE_SHARED_LIBRARY_PREFIX}FreeImageLite${PGE_LIBS_DEBUG_SUFFIX}${CMAKE_SHARED_LIBRARY_SUFFIX}")
    set(USE_STATIC_FREEIMAGE OFF)
else()
    if(CMAKE_STATIC_LIBRARY_PREFIX STREQUAL "" AND CMAKE_STATIC_LIBRARY_SUFFIX STREQUAL ".lib")
        set(FI_STATIC_SUFFIX "-static")
    else()
        set(FI_STATIC_SUFFIX "")
    endif()
    set(libFreeImage_Libs "${DEPENDENCIES_INSTALL_DIR}/lib/${CMAKE_STATIC_LIBRARY_PREFIX}FreeImageLite${FI_STATIC_SUFFIX}${PGE_LIBS_DEBUG_SUFFIX}${CMAKE_STATIC_LIBRARY_SUFFIX}")
    set(USE_STATIC_FREEIMAGE ON)
endif()

ExternalProject_Add(
    FreeImage_Local
    PREFIX ${CMAKE_BINARY_DIR}/external/FreeImage
    DOWNLOAD_COMMAND ""
    SOURCE_DIR ${CMAKE_SOURCE_DIR}/_Libs/FreeImage
    CMAKE_ARGS
        "-DCMAKE_INSTALL_PREFIX=${DEPENDENCIES_INSTALL_DIR}"
        "-DFREEIMAGE_SHARED=${USE_SHARED_FREEIMAGE}"
        "-DFREEIMAGE_STATIC=${USE_STATIC_FREEIMAGE}"
        "-DDEPENDENCIES_INSTALL_DIR=${DEPENDENCIES_INSTALL_DIR}"
        "-DCMAKE_PROJECT_FreeImage_INCLUDE=${CMAKE_SOURCE_DIR}/_common/build_env.cmake"
        "-DCMAKE_TOOLCHAIN_FILE=${CMAKE_TOOLCHAIN_FILE}"
        "-DCMAKE_INSTALL_PREFIX=${DEPENDENCIES_INSTALL_DIR}"
        "-DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}"
        "-DCMAKE_CONFIGURATION_TYPES=${CMAKE_CONFIGURATION_TYPES}"
        "-DCMAKE_POSITION_INDEPENDENT_CODE=ON"
        "-DCMAKE_DEBUG_POSTFIX=${PGE_LIBS_DEBUG_SUFFIX}"
        "-DFREEIMAGE_USE_SYSTEM_LIBPNG=ON"
        "-DFREEIMAGE_USE_SYSTEM_LIBJPEG=ON"
        "-DFREEIMAGE_USE_SYSTEM_ZLIB=ON"
        "-DFREEIMAGE_PNG_INCLUDE=${PNG_INCLUDE_DIRS}"
        "-DFREEIMAGE_ZLIB_INCLUDE=${ZLIB_INCLUDE_DIRS}"
        "-DFREEIMAGE_PNG_LIB=${libPNG_A_Lib}"
        "-DFREEIMAGE_ZLIB_LIB=${libZLib_A_Lib}"
        "-DFREEIMAGE_JPEG_INCLUDE=${JPEG_INCLUDE_DIRS}"
        "-DFREEIMAGE_JPEG_LIB=${libJPEG_A_Lib}"
        ${ANDROID_CMAKE_FLAGS}
        ${APPLE_CMAKE_FLAGS}
    BUILD_BYPRODUCTS
        "${libFreeImage_Libs}"
)

target_link_libraries(PGE_FreeImage INTERFACE
    "${libFreeImage_Libs}"
    PGE_libPNG
    PGE_ZLib
    PGE_libJPEG
)

if(USE_SHARED_FREEIMAGE AND NOT WIN32)
    install(FILES ${libFreeImage_Libs} DESTINATION ${PGE_INSTALL_DIRECTORY})
endif()
